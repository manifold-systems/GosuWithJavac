plugins {
    id 'java'
}

group 'systems.manifold'
version '1.0-SNAPSHOT'

targetCompatibility = 11
sourceCompatibility = 11

repositories {
    mavenLocal()
    jcenter()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

configurations {
    // give tests access to compileOnly dependencies
    testImplementation.extendsFrom compileOnly
}

dependencies {
    runtime 'org.gosu-lang.gosu:gosu-core:1-X-SNAPSHOT'
    implementation 'org.gosu-lang.gosu:gosu-core-api-precompiled:1-X-SNAPSHOT'

    // Add manifold to -processorpath for javac
    annotationProcessor 'org.gosu-lang.gosu:gosu-core:1-X-SNAPSHOT'
}

if (JavaVersion.current() != JavaVersion.VERSION_1_8 &&
        sourceSets.main.allJava.files.any {it.name == "module-info.java"}) {
    tasks.withType(JavaCompile) {
        // if you DO define a module-info.java file:
        options.compilerArgs += ['-Xplugin:Manifold no-bootstrap', '--module-path', it.classpath.asPath]
        options.compilerArgs += ['-Amanifold.source.gs=foo\\..*']
        options.compilerArgs += ['-Amanifold.source.gsp=foo\\..*']
    }
} else {
    tasks.withType(JavaCompile) {
        // If you DO NOT define a module-info.java file:
        options.compilerArgs += ['-Xplugin:Manifold no-bootstrap']
        options.compilerArgs += ['-Amanifold.source.gs=foo\\..*']
        options.compilerArgs += ['-Amanifold.source.gsp=foo\\..*']
    }
}
